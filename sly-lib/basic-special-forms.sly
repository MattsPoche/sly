(require "sly-lib/list.sly")
(require "sly-lib/quasiquote.sly")
(require "sly-lib/syntax-case.sly")
(require "sly-lib/syntax-rules.sly")
(require "sly-lib/with-syntax.sly")

(provide and
		 or
		 let
		 let*
		 letrec
		 cond
		 when
		 unless)

(define-syntax and
  (syntax-rules ()
	[(_ x) x]
	[(_ x1 x2 ...)
	 (if x1 (and x2 ...) #f)]))

(define-syntax or
  (syntax-rules ()
	[(_ x) x]
	[(_ x1 x2 ...)
	 (if x1 #t (or x2 ...))]))

(define-syntax let
  (syntax-rules ()
	[(_ ([var val] ...) body ...)
	 ((lambda (var ...) body ...) val ...)]
	[(_ name ([var val] ...) body ...)
	 ((lambda ()
		(define name (lambda (var ...) body ...))
		(name val ...)))]))

(define-syntax let*
  (syntax-rules ()
	[(_ () body ...)
	 (let () body ...)]
	[(_ ([var val] [var* val*] ...) body ...)
	 (let ([var val])
	   (let* ([var* val*] ...) body ...))]))

(define-syntax letrec*-helper
  (syntax-rules ()
	[(_ () body ...)
	 (begin body ...)]
	[(_ ([var val] rest ...) body ...)
	 (begin
	   (define var val)
	   (letrec*-helper rest ...) body...)]))

(define-syntax letrec*
  (syntax-rules ()
	[(_ bindings body ...)
	 ((lambda ()
		(letrec*-helper bindings body ...)))]))

(define-syntax letrec
  (lambda (x)
	(syntax-case x ()
	  [(letrec ([id val] ...) body ...)
	   (with-syntax ([(tmp ...) (generate-temporaries #'(id ...))]
					 [(id ...) #'(id ...)]
					 [(val ...) #'(val ...)]
					 [(body ...) #'(body ...)])
		 #'(let ([id #f] ...)
			 (let ([tmp val] ...)
			   (set! id tmp) ...
			   (let () body ...))))])))

(define-syntax cond
  (lambda (x)
    (syntax-case x ()
      [(_ c1 c2 ...)
       (let f ([c1 #'c1]
			   [cmore (syntax->list #'(c2 ...))])
         (if (null? cmore)
             (syntax-case c1 (else =>)
               [(else e1 e2 ...) #'(begin e1 e2 ...)]
               [(e0 => e1) #'(let ([t e0]) (if t (e1 t) (void)))]
               [(e0 e1 e2 ...) #'(if e0 (begin e1 e2 ...) (void))]
			   [(e0) #'(let ([t e0]) (if t t))])
             (with-syntax ([rest (f (car cmore) (cdr cmore))])
               (syntax-case c1 (=>)
                 [(e0 => e1) #'(let ([t e0]) (if t (e1 t) rest))]
                 [(e0 e1 e2 ...)
                  #'(if e0 (begin e1 e2 ...) rest)]
				 [(e0) #'(let ([t e0]) (if t t rest))]))))])))

;;; TODO: Implement `do' form.

(define-syntax when
  (syntax-rules ()
	[(_ condition body ...)
	 (if condition
		 (begin body ...)
		 (void))]))

(define-syntax unless
  (syntax-rules ()
	[(_ condition body ...)
	 (if condition
		 (void)
		 (begin body ...))]))
